# -*- python -*-
Import('env')

buildEnv = env.Clone()
buildEnv.Append(LIBPATH=['.'])

sources = buildEnv.Glob('[!Q]*.cc')
libs = ['dupr',
        'boost_system',
        'boost_chrono',
        'boost_timer',
        'boost_thread',
        'boost_filesystem',
        'boost_program_options',
       ]
buildEnv.StaticLibrary('dupr', sources)
for target in ('qserv-htm-index',
               'qserv-partition',
               'qserv-partition-matches',
              ):
    source = ''.join(map(lambda s: s.title(), target.split('-'))) + '.cc'
    program = buildEnv.Program(target, [source], LIBS=libs)
    buildEnv.Install('$PREFIX/bin', program)
