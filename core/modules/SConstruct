# -*- python -*-
#
# Setup our environment
#
# Do not change these
import glob, os, re, sys
from itertools import chain, izip, repeat
import SCons.Util
import detect # Qserv dep detector
##import detect_deps
env = Environment(tools=["default", "protoc", "antlr"])

# Describe what your package contains here.
env.Help("""
Qserv core code
""")

# Detect protobufs
##detect_deps.detectProtobufs()

# Handle debug
debug = ARGUMENTS.get('debug', 0) 
print "debug is", debug
if int(debug):
    print "Debug build requested."
    env.Append(CCFLAGS = '-g')

# Environment
if not env.GetOption('clean'):
    detect.addExtern(env, ARGUMENTS.get('extern', None))
    env.Append(CPPPATH=[os.getcwd()]) # top level
    env.Append(CPPPATH=[env['PROTOC_INC']])
    detect.setXrootd(env)
    env.Append(CPPFLAGS=["-D_FILE_OFFSET_BITS=64", "-fPIC"])

# Build dir
bldDir = ARGUMENTS.get('bld', None)
if bldDir:
    env.VariantDir(bldDir, ".")
    env.Append(variant_dir=bldDir)

# Products
shProducts = { "xrdfs" : {'mods' : """proto xrdfs 
                                      wbase wconfig wcontrol wdb wsched
                                      log global""".split(),
                          'libs' : """ssl crypto 
                                      boost_regex boost_thread 
                                      boost_signals
                                      XrdSys XrdServer XrdClient
                                      XrdOfs XrdSfs XrdUtils""".split()},
      
               "xrdoss" : {'mods' : ["xrdoss"],
                           'libs' : """ssl crypto""".split()},
               "_masterlib" : {'mods' : """czar parser query
                                           log merger meta mysql util xrdc
                                           obsolete
                                           qana qdisp qproc""".split(),
                               'libs' : """ssl crypto 
                                      boost_regex boost_thread 
                                      boost_signals""".split() }
               }
modList = [m for m in chain(*map(lambda d: d['mods'], 
                                shProducts.values()))] 

libProducts = dict(izip(modList, repeat(None)))
mRoot = "."
libDepName = "extLibs"
def getLibDeps(mName):
    try:
        libDeps = open(os.path.join(mRoot, mName, libDepName)).read()
        return libDeps
    except IOError, e:
        #print "Warning, no %s for module %s" % (libDepName, mName)
        return []

def getSrcs(mName):
    srcs = os.listdir(os.path.join(mRoot,mName))
    return map(lambda s: os.path.join(mRoot, mName, s), srcs)

extraTgts = {}
def getTgts(mName):
    scriptPath = os.path.join(mRoot, mName, "SConscript")
    if os.access(scriptPath, os.R_OK):
        tgts = SConscript(os.path.join(mRoot, mName, "SConscript"),
                          exports={'env': env, 'extraTgts': extraTgts})
        return tgts
    else:
        #print "No targets for %s, using glob" % mName
        return Glob(os.path.join(mRoot, mName, "*.cc"))

boostChecker = detect.BoostChecker(env)
def findLibs(libList):
    def cheapCheckLib(libName):
        if libName.startswith("boost"):
            return boostChecker.getLibName(libName)
        else: return libName
    return map(cheapCheckLib, libList)

########################################################################
## Real work now.
########################################################################

# Gather up source files
for mod in libProducts:
    objs = getTgts(mod)
    libProducts[mod] = objs

# Compute shlib targets 
for (pkg, opts) in shProducts.items():
    srcs = []
    libs = []
    for m in opts['mods']: # Visit modules and compose union of LIBS
        libs.extend(getLibDeps(m))
        prods = libProducts[m]
        srcs.extend(prods)
        pass
    
    # eliminate dupes for srcs and libs
    libs = [a for a in set(libs)]
    srcs = [a for a in set(srcs)]
    env.Append(LIBS=libs)
    #extLibFlags = ["-l"+lib for lib in findLibs(opts['libs'])]
    extLibs = [lib for lib in findLibs(opts['libs'])]

    env.SharedLibrary(pkg + ".so", srcs, LIBS=extLibs)

# Install targets

# Tests

#
# Build/install things
#

#####################################################################
# Variant support
#    VariantDir(bldDir, 'src')               
#    SConscript("src/SConscript.lib", variant_dir=bldDir,
#               exports={'env' : expEnv, 'detect_deps' : detect_deps})

# for d in Split("proto src tests doc"): 
#     if os.path.isdir(d):
#         try:
#             SConscript(os.path.join(d, "SConscript"), 
#                        exports={'env' : env})
#         except Exception, e:
#             print >> sys.stderr, "%s: %s" % (os.path.join(d, "SConscript"), e)
